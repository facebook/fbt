/*
 * Copyright (c) Meta Platforms, Inc. and affiliates.
 *
 * This source code is licensed under the MIT license found in the
 * LICENSE file in the root directory of this source tree.
 */

// android/build.gradle

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

buildscript {
    // The Android Gradle plugin is only required when opening the android folder stand-alone.
    // This avoids unnecessary downloads and potential conflicts when the library is included as a
    // module dependency in an application project.
    if (project == rootProject) {
        repositories {
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:4.2.2'
        }
    }
}

apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

// Matches values in recent template from React Native 0.65
// https://github.com/facebook/react-native/blob/0.65-stable/template/android/build.gradle#L4-L10
def DEFAULT_COMPILE_SDK_VERSION = 30
def DEFAULT_MIN_SDK_VERSION = 21
def DEFAULT_TARGET_SDK_VERSION = 30

android {
    compileSdkVersion safeExtGet('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../node_modules/react-native/android"
    }
    maven {
        // Android JSC is installed from npm
        url "$rootDir/../node_modules/jsc-android/dist"
    }
    google()
    mavenCentral()
}

dependencies {
    // ref:
    // https://github.com/facebook/react-native/blob/0.65-stable/template/android/app/build.gradle#L184
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'  // From node_modules
}

afterEvaluate {
    task installArchives {
        dependsOn("publishAllPublicationsToMavenRepository")
    }

    task sourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        archiveClassifier = 'sources'
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    def packageJson = new groovy.json.JsonSlurper().parseText(file('../package.json').text)

    publishing {
        repositories {
            maven {
                url = uri("file://$projectDir/../android/maven")
            }
        }
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = packageJson.name
                groupId = "com.facebook.react.modules"
                version = packageJson.version

                artifact(sourcesJar)
                artifact(javadocJar)

                pom {
                    name = packageJson.title
                    description = packageJson.description
                    url = packageJson.baseUrl

                    developers {
                        developer {
                            id = "facebook"
                            name = "Facebook"
                        }
                    }
                    licenses {
                        license {
                            name = packageJson.license
                            url = packageJson.repository.baseUrl + '/blob/master/' + packageJson.licenseFilename
                            distribution = "repo"
                        }
                    }
                }
            }
        }
    }
}
